CLEAR
CLS
ACLS
GCLS

'FOR BEZIER CURVE
BEZSTEP=10
BEZCTRL=5

DIM POS[BEZCTRL*2 ,2]
DIM ANSPT[(BEZCTRL-1)*BEZSTEP+1,2]

'
SPSET 0,1,0,0,0,0,16,16
SPHOME 0,8,8

POS[0,0] = 51
POS[0,1] = 140
POS[1,0] = 61
POS[1,1] = 10

POS[2,0] = 190
POS[2,1] = 30
POS[3,0] = 120
POS[3,1] = 130

POS[4,0] = 220
POS[4,1] = 51
POS[5,0] = 188
POS[5,1] = 140

POS[6,0] = 248
POS[6,1] = 164
POS[7,0] = 248
POS[7,1] = 105

POS[8,0] = 65
POS[8,1] = 150
POS[9,0] = 65
POS[9,1] = 109




SELTARGET=0

'GOSUB @UPDATEBEZ
GOSUB @POSDRAW
@MAINLOOP

MOVX=0:MOVY=0:MOVFLG=FALSE
IF BTRIG() AND &H80 THEN GOSUB @KEYY
IF BTRIG() AND &H10 THEN GOSUB @KEYA

CURIDX=SELTARGET*2


LPPOS=0.5
IF BUTTON() AND &H20 THEN LPPOS=0
IF BUTTON() AND &H40 THEN LPPOS=1
IF BUTTON() AND &H01 THEN MOVY=-1:MOVFLG=TRUE
IF BUTTON() AND &H02 THEN MOVY=1:MOVFLG=TRUE
IF BUTTON() AND &H04 THEN MOVX=-1:MOVFLG=TRUE
IF BUTTON() AND &H08 THEN MOVX=1:MOVFLG=TRUE
IF MOVFLG==TRUE THEN GOSUB @POSUPDATE

V1X=POS[CURIDX,0]
V2X=POS[CURIDX+1,0]
V1Y=POS[CURIDX,1]
V2Y=POS[CURIDX+1,1]
LEAPSTEP=LPPOS
GOSUB @VECLEAP
SPOFS 0,VAX,VAY

FOR I=0 TO BEZCTRL*2-1
LOCATE 0,I:PRINT POS[I,0] , POS[I,1]
NEXT I

VSYNC 1
GOTO @MAINLOOP


END

@KEYY
IF SELTARGET == 0 THEN SELTARGET=BEZCTRL-1 ELSE SELTARGET=SELTARGET-1
RETURN


@KEYA
IF SELTARGET == BEZCTRL-1 THEN SELTARGET=0 ELSE SELTARGET=SELTARGET+1
RETURN


@POSUPDATE

GCLS
MULPOS=1
IF BUTTON() AND &H200 THEN MULPOS=10

IF LPPOS == 0 THEN GOSUB @POSP1MOV:GOTO @POSDRAW
IF LPPOS == 1 THEN GOSUB @POSP2MOV:GOTO @POSDRAW
GOSUB @POSP1MOV:GOSUB @POSP2MOV
GOTO @POSDRAW

@POSP1MOV
POS[CURIDX,0] = POS[CURIDX,0] + (MOVX*MULPOS)
POS[CURIDX,1] = POS[CURIDX,1] + (MOVY*MULPOS)
RETURN

@POSP2MOV
POS[CURIDX+1,0] = POS[CURIDX+1,0] + (MOVX*MULPOS)
POS[CURIDX+1,1] = POS[CURIDX+1,1] + (MOVY*MULPOS)
RETURN

@POSDRAW
FOR I=0 TO BEZCTRL-1
GLINE POS[I*2+0,0],POS[I*2+0,1],POS[I*2+1,0],POS[I*2+1,1],12
NEXT I
GOSUB @UPDATEBEZ
RETURN


@UPDATEBEZ
GOSUB @BEZIERCURVE
GOSUB @BEZDRAW
RETURN


@BEZERASE
GOTO @BEZDRAWMAIN

'DRAW BEZIER CURVE
'IN:
' ANSPOS[,]
' BEZCTRL
' BEZSTEP
@BEZDRAW
'GDRAWMD FALSE
@BEZDRAWMAIN
FOR I=0 TO (BEZCTRL-1)*BEZSTEP-1
GLINE ANSPT[I,0],ANSPT[I,1],ANSPT[I+1,0],ANSPT[I+1,1],15
NEXT I

RETURN



'FUNC LEAP
'IN:
' V1 
' V2 
' LEAPSTEP
'OUT:
' VA
'USE:
' 
@LEAP
VA=V1 + (V2-V1) * LEAPSTEP
RETURN


'FUNC VECTOR LEAP
'IN:
' V1X,V1Y
' V2X,V2Y
' LEAPSTEP
'OUT:
' VAX,VAY
'USE:
'
@VECLEAP
V1=V1X
V2=V2X
GOSUB @LEAP
VAX=VA
V1=V1Y
V2=V2Y
GOSUB @LEAP
VAY=VA
RETURN


'FUNC BEZIER CURVE
'IN:
' POS[4,2]
' BEZSTEP
, BEZCTRL
'OUT:
' ANSPT[BEZSTEP+1,2]
'USE:
'
@BEZIERCURVE
FOR J=0 TO BEZCTRL-2
V1X=POS[J*2+0,0]
V1Y=POS[J*2+0,1]
V2X=POS[J*2+1,0]
V2Y=POS[J*2+1,1]
LEAPSTEP=0.5
GOSUB @VECLEAP
SPX=VAX
SPY=VAY

P1X=POS[J*2+1,0]
P1Y=POS[J*2+1,1]
P2X=POS[J*2+2,0]
P2Y=POS[J*2+2,1]

V1X=POS[J*2+2,0]
V1Y=POS[J*2+2,1]
V2X=POS[J*2+3,0]
V2Y=POS[J*2+3,1]
LEAPSTEP=0.5
GOSUB @VECLEAP
EPX=VAX
EPY=VAY

BSLICE = 1/BEZSTEP

FOR I=0 TO BEZSTEP-1
LEAPSTEP=BSLICE*I

V1X=SPX
V1Y=SPY
V2X=P1X
V2Y=P1Y
GOSUB @VECLEAP
TMP1X=VAX
TMP1Y=VAY

V1X=P1X
V1Y=P1Y
V2X=P2X
V2Y=P2Y
GOSUB @VECLEAP
TMP2X=VAX
TMP2Y=VAY

V1X=P2X
V1Y=P2Y
V2X=EPX
V2Y=EPY
GOSUB @VECLEAP
TMP3X=VAX
TMP3Y=VAY

V1X=TMP1X
V1Y=TMP1Y
V2X=TMP2X
V2Y=TMP2Y
GOSUB @VECLEAP
TMP1X=VAX
TMP1Y=VAY

V1X=TMP2X
V1Y=TMP2Y
V2X=TMP3X
V2Y=TMP3Y
GOSUB @VECLEAP
TMP2X=VAX
TMP2Y=VAY

V1X=TMP1X
V1Y=TMP1Y
V2X=TMP2X
V2Y=TMP2Y
GOSUB @VECLEAP
ANSPT[J*BEZSTEP+I,0]=VAX
ANSPT[J*BEZSTEP+I,1]=VAY
NEXT I

NEXT J


ANSPT[J*BEZSTEP,0]=EPX
ANSPT[J*BEZSTEP,1]=EPY

RETURN

'---END--
